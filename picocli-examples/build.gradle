description 'Java and Groovy examples for the picocli project.'

sourceCompatibility = 1.8 // for the JSR-380 BeanValidation example
targetCompatibility = 1.8
dependencies {
    compile rootProject
    compile "org.apache.ivy:ivy:$ivyVersion", // for Intelli/J
            "org.codehaus.groovy:groovy-all:$groovyVersion",

            // for the JSR-380 BeanValidation example
            "javax.validation:validation-api:2.0.0.Final",
            "org.hibernate.validator:hibernate-validator:6.0.2.Final",
            "org.hibernate.validator:hibernate-validator-annotation-processor:6.0.2.Final",
            "javax.el:javax.el-api:3.0.0",
            "org.glassfish.web:javax.el:2.2.6"
}

def generatedResources = "$buildDir/generated-resources/main"
sourceSets {
    main {
        //register an output folder on the main SourceSet:
        output.dir(generatedResources, builtBy: 'generateVersionTxt')
        //it is now a part of the 'main' classpath and will be a part of the jar
    }
}

//a task that generates the resources for the example VersionProviderDemo1:
task generateVersionTxt {
    description 'Creates a version.txt file with build info that is added to the root of the picocli-examples jar'
    doLast {
        new File(generatedResources).mkdirs()
        def generated = new File(generatedResources, "version.txt")
        generated.text = """
Version: $rootProject.version
Buildtime: ${new java.text.SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())}
Application-name: $rootProject.name $project.name
"""
    }
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' <<
//            '-processorpath' << 'C:\\Users\\remko\\IdeaProjects\\picocli3\\picocli-codegen\\build\\libs\\picocli-codegen-4.0.0-SNAPSHOT.jar;C:\\Users\\remko\\IdeaProjects\\picocli3\\build\\libs\\picocli-4.0.0-SNAPSHOT.jar' <<
//            '-processor' << 'picocli.codegen.annotation.processing.AnnotatedCommandSourceGeneratorProcessor'
//}
