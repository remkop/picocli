group 'info.picocli'
description 'Annotation-based Java command line parser. Usage help with ANSI styles and colors. Autocomplete. Nested subcommands. Easily included as source to avoid adding a dependency.'
version '3.0.0-alpha-4'

// for bumpVersion task
def oldVersion = '3\\.0\\.0-alpha-4-SNAPSHOT'
def oldRevdate = '2018\\-03\\-31'
def revDate    = '2018-04-03'

// for bumpReadmeVersion task
def previousReleaseVersion = '3.0.0-alpha-3'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

allprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    repositories {
        jcenter()
    }

    configurations {
        ivy
    }
    dependencies {
        compileOnly 'org.codehaus.groovy:groovy-all:2.4.10'
        ivy         'org.apache.ivy:ivy:2.4.0' // for Gradle
        testCompile 'junit:junit:4.12',
                    'org.hamcrest:hamcrest-core:1.3',
                    'org.fusesource.jansi:jansi:1.15',
                    'org.codehaus.groovy:groovy-all:2.4.10',
                    'com.github.stefanbirkner:system-rules:1.17.1'

    }
    tasks.withType(GroovyCompile) {
        // this, and the `configurations {ivy}` section, are a workaround for the dreaded
        // java.lang.NoClassDefFoundError: org/apache/ivy/core/report/ResolveReport
        // that occurs when trying to compile a groovy script containing a @Grab annotation in gradle.
        // see https://stackoverflow.com/questions/18173908/error-compiling-a-groovy-project-using-grab-annotation
        groovyClasspath += configurations.ivy
    }
}
project('examples') {
    dependencies {
        compile rootProject
        compile 'org.apache.ivy:ivy:2.4.0', // for Intelli/J
                'org.codehaus.groovy:groovy-all:2.4.10'
    }
    def generatedResources = "$buildDir/generated-resources/main"
    sourceSets {
        main {
            //register an output folder on the main SourceSet:
            output.dir(generatedResources, builtBy: 'generateVersionTxt')
            //it is now a part of the 'main' classpath and will be a part of the jar
        }
    }

    //a task that generates the resources for the example VersionProviderDemo1:
    task generateVersionTxt {
        description 'Creates a version.txt file with build info that is added to the root of the examples jar'
        doLast {
            new File(generatedResources).mkdirs()
            def generated = new File(generatedResources, "version.txt")
            generated.text = """
Version: $rootProject.version
Buildtime: ${new java.text.SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())}
Application-name: $rootProject.name $project.name
"""
        }
    }
}
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'jacoco'
apply plugin: 'distribution'
apply plugin: 'maven-publish'


jar {
    manifest {
        attributes 'Specification-Title'   : 'picocli',
                   'Specification-Vendor'  : 'Remko Popma',
                   'Specification-Version' : version,
                   'Implementation-Title'  : 'picocli',
                   'Implementation-Vendor' : 'Remko Popma',
                   'Implementation-Version': version,
                   'Main-Class'            : 'picocli.AutoComplete',
                   'Automatic-Module-Name' : 'info.picocli'
    }
}
javadoc {
    options.overview = "src/main/java/overview.html"
    destinationDir   = file("build/docs/apidocs")
}
javadoc.dependsOn('asciidoctor')

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
task testJar(type: Jar, dependsOn: compileTestJava) {
    from sourceSets.test.output
    classifier = 'tests'
}
task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task testSourcesJar(type: Jar) {
    from sourceSets.test.java.srcDirs
    classifier = 'test-sources'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
asciidoctor {
    sourceDir = file('docs')
    outputDir = file('build/docs')
    logDocuments = true
}
artifacts {
    archives javadocJar
    archives sourcesJar
    archives testSourcesJar
    archives testJar
    archives jar
}
task bumpReadmeVersion {
    doLast {
        // README.md
        ant.replaceregexp(match: "$previousReleaseVersion", replace: "$version", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}
task bumpVersion {
    doLast {
        ant.replaceregexp(match: "\"$oldVersion\"", replace: "\"$version\"", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'src/main/java/picocli', includes: 'CommandLine.java')
            fileset(dir: 'src/test/java/picocli', includes: 'CommandLineTest.java')
        }
        ant.replaceregexp(match: "version $oldVersion", replace: "version $version", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'src/test/java/picocli', includes: 'AutoCompleteTest.java')
        }
        // Doc header
        ant.replaceregexp(match: ":revnumber: $oldVersion", replace: ":revnumber: $version", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
            fileset(dir: 'docs', includes: 'autocomplete.adoc')
        }
        // Downloads section, Gradle
        ant.replaceregexp(match: ":picocli:$oldVersion", replace: ":picocli:$version", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
        }
        // Downloads section, Maven
        ant.replaceregexp(match: "<version>$oldVersion</version>", replace: "<version>$version</version>", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
        }
        // Downloads section, SBT
        ant.replaceregexp(match: "\"picocli\" % \"$oldVersion\"", replace: "\\\"picocli\\\" % \\\"$version\\\"", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
        }
        // Downloads section, Ivy
        ant.replaceregexp(match: "rev=\"$oldVersion\"", replace: "rev=\\\"$version\\\"", flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
        }
        ant.replaceregexp(match: oldRevdate, replace: revDate, flags: 'g', byline: true, encoding: 'UTF8') {
            fileset(dir: 'docs', includes: 'index.adoc')
            fileset(dir: 'docs', includes: 'autocomplete.adoc')
        }
    }
}
task copyDocs(type: Copy) {
    from('build/docs/html5/') { include '*.html' }
    from('build/docs/')       { exclude 'html5'}
    into 'docs'
}

distributions {
    main {
        baseName = "$archivesBaseName-all"
        contents {
            from jar
            from sourcesJar
            from testJar
            from testSourcesJar
            from javadocJar
            from ('LICENSE')
            from ('RELEASE-NOTES.md')
        }
    }
}
ext {
    bintrayBaseUrl    = 'https://api.bintray.com/maven'
    bintrayRepository = 'picocli'
    bintrayPackage    = 'picocli'
    bintrayUsername   = System.getenv('BINTRAY_USER')
    bintrayApiKey     = System.getenv('BINTRAY_KEY')
}
publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact testJar
            artifact testSourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('packaging',     'jar')
                root.appendNode('name',          'picocli - a mighty tiny Command Line Interface')
                root.appendNode('description',   description)
                root.appendNode('url',           'http://picocli.info')
                root.appendNode('inceptionYear', '2017')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name',         'The Apache Software License, version 2.0')
                license.appendNode('url',          'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id',    'rpopma')
                developer.appendNode('name',  'Remko Popma')
                developer.appendNode('email', 'rpopma@apache.org')

                def scm = root.appendNode('scm')
                scm.appendNode('connection',          'scm:git:https://github.com/remkop/picocli.git')
                scm.appendNode('developerConnection', 'scm:git:ssh://github.com:remkop/picocli.git')
                scm.appendNode('url',                 'https://github.com/remkop/picocli/tree/master')
            }
        }
    }
    repositories {
        maven {
            name 'myLocal'
            url "file://$projectDir/../repo/$bintrayUsername"
        }
        maven {
            name 'Bintray'
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"
            credentials {
                username = bintrayUsername
                password = bintrayApiKey
            }
        }
    }
}
/*
Release procedure:
1. edit version numbers: remove -SNAPSHOT classifier
2. gradlew bumpVersion
3. check modified files
4. gradlew clean build
5. gradlew copyDocs
6. update RELEASE-NOTES.md
7. gradlew bumpReadmeVersion
7a update README.md (latest version, release notes)
8. commit -m "Release picocli version ..."
9. tag v$version
10. gradlew publishPluginPublicationToBintrayRepository

11. edit version numbers: increase minor version and add -SNAPSHOT classifier
12. gradlew bumpVersion
13. check modified files
14. commit -m "Prepare for next development cycle"
15. push (make sure that Push Tags is checked)

16. Log in to GitHub, go to https://github.com/remkop/picocli/releases
17. Click the new tag, click Edit button, update title and release notes (copy from RELEASE-NOTES.md)
18. Upload picocli-$version.jar and picocli-all$version.zip to GitHub

19. Log in to Bintray
20. Navigate to the page for the new version
21. Edit version: Publication Date, Description, VCS tag, GitHub release notes file (RELEASE-NOTES.md)
22. On the version page, Release Notes tab, select GitHub File
23. Publish artifacts to JCenter
24. On the version page, Maven Central tab, sync to Maven (takes several minutes)

(When releasing from branch)
25. Switch to master
26. Update RELEASE-NOTES.md (insert changes from branch)
27. Update last release version in README
28. Update `previousReleaseVersion` in build.gradle
29. gradlew copyDocs
30. commit -m "Update master for release x.x (from branch x.x)"
*/
